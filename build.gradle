import org.gradle.internal.os.OperatingSystem

group 'com.mhaqs.openrndr.processing'
version '0.1'

buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext.rndr_version = '0.3.36-rc3'
ext.panel_version = '0.3.16'
ext.slf4j_version = '1.7.25'
ext.coroutines_version = '1.3.2'
ext.rndr_gl3_natives = ""
ext.orx_version = "0.3.37"

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-windows"
        break
    case OperatingSystem.MAC_OS:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-macos"
        break
    case OperatingSystem.LINUX:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-linux-x64"
        break

}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'application'
    mainClassName = 'ExampleKt'
    applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
    tasks.run.workingDir = new File('.').absolutePath

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/openrndr/openrndr/"
        }
        maven { url 'https://jitpack.io' }

    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        compile "org.openrndr:openrndr-core:$rndr_version"
        compile "org.openrndr:openrndr-animatable:$rndr_version"
        compile "org.openrndr:openrndr-svg:$rndr_version"
        compile "org.openrndr:openrndr-extensions:$rndr_version"
        compile "com.github.openrndr.orx:orx-no-clear:$orx_version"
        compile "com.github.openrndr.orx:orx-noise:$orx_version"
        runtime "org.openrndr:openrndr-gl3:$rndr_version"
        runtime "org.openrndr:$rndr_gl3_natives:$rndr_version"
        compile 'org.slf4j:slf4j-nop:1.7.25'
        implementation "com.github.openrndr:openrndr-panel:v$panel_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

